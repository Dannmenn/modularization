plugins {
    id 'java'
    id 'application'
}

group 'pl.mendroch.modularization.test'
version '1.0-SNAPSHOT'

application {
    mainClassName = "pl.mendroch.modularization.example/pl.mendroch.modularization.example.ExampleEntryPoint"
}

dependencies {
    implementation project("service")
    dynamicImplementation project("provider")
    dynamicImplementation "org.apache.commons:commons-lang3:3.3.2"
}

compileJava {
    options.compilerArgs.addAll("--module-path", classpath.asPath)
}

distZip {
    into("$applicationName-$version/lib") {
        from configurations.dynamicImplementation - configurations.runtimeClasspath
    }
}

jar {
    metaInf {
        def resFile = new File(buildDir, 'generated/resources/META-INF/')
        resFile.mkdirs()
        def file = new File(resFile, 'dependencies.properties')
        file.text = "#DYNAMIC_IMPLEMENTATION\n"
        project
                .getConfigurations()
                .getByName("dynamicImplementation")
                .resolvedConfiguration
                .firstLevelModuleDependencies
                .each {
                    file.text += it.moduleGroup + "\\:" + it.moduleName + "=" + it.moduleVersion + "\n"
                }
        file.text += "#DYNAMIC_RUNTIME\n"
        from(file)
    }
    manifest {
        attributes(
                "Name": project.group + ":" + project.name,
                "Specification-Version": version
        )
    }
}
