plugins {
    id 'java'
    id 'application'
}

group 'pl.mendroch.modularization.test'

sourceCompatibility = 12
targetCompatibility = 12

application {
    mainClassName = "pl.mendroch.modularization.example/pl.mendroch.modularization.example.ExampleEntryPoint"
}

repositories {
    mavenCentral()
}

configurations {
    dynamicImplementation.extendsFrom implementation
}

dependencies {
    implementation project("service")
    dynamicImplementation project("provider")
}

compileJava {
    options.compilerArgs.addAll("--module-path", classpath.asPath)
}

distZip {
    into("$applicationName/lib") {
        from configurations.dynamicImplementation - configurations.runtimeClasspath
    }
}

jar {
    metaInf {
        def resFile = new File(buildDir, 'generated/resources/META-INF/')
        resFile.mkdirs()
        def file = new File(resFile, 'dependencies.properties')
        file.text = "#DYNAMIC_IMPLEMENTATION\n"
        project
                .getConfigurations()
                .getByName("dynamicImplementation")
                .resolvedConfiguration
                .firstLevelModuleDependencies
                .each {
                    file.text += it.moduleGroup + "\\:" + it.moduleName + "=" + it.moduleVersion + "\n"
                }
        file.text += "#DYNAMIC_RUNTIME\n"
        from(file)
    }
}
