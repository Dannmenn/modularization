plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

group 'pl.mendroch.modularization'
version '1.0-SNAPSHOT'
ext.moduleName = 'pl.mendroch.modularization.example.javafx'

application {
    mainClassName = "pl.mendroch.modularization.example.javafx.JavaFXApplication"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly project(":common")
    compileOnly project(":core")
    dynamicImplementation project("javafx-example-api")
    uses project("javafx-sample-view")
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.0.0'
    compileOnly group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.3.0.Final'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

javafx {
    version = "12.0.2"
    modules = ['javafx.controls']
}

jar {
    from configurations.dynamicImplementation - configurations.runtimeClasspath
    metaInf {
        def resFile = new File(buildDir, 'generated/resources/META-INF/')
        resFile.mkdirs()
        def file = new File(resFile, 'dependencies.properties')
        file.text = "#DYNAMIC_IMPLEMENTATION\n"
        project
                .getConfigurations()
                .getByName("dynamicImplementation")
                .resolvedConfiguration
                .firstLevelModuleDependencies
                .each {
                    file.text += it.moduleGroup + "\\:" + it.moduleName + "=" + it.moduleVersion + "\n"
                }
        file.text += "#DYNAMIC_RUNTIME\n"
        project
                .getConfigurations()
                .getByName("uses")
                .resolvedConfiguration
                .firstLevelModuleDependencies
                .each {
                    file.text += "R\\:" + it.moduleGroup + "\\:" + it.moduleName + "=" + it.moduleVersion + "\n"
                }
        from(file)
    }
    manifest {
        attributes(
                "Name": project.group + ":" + project.name,
                "Main-Class": "pl.mendroch.modularization.example.javafx.JavaFXApplication",
                "Specification-Version": version
        )
    }
}